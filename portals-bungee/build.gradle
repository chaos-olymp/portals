plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
    id 'kr.entree.spigradle.bungee' version '2.1.1'
}

group 'de.chaosolymp'
version rootProject.version

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    implementation project(":portals-core")
    implementation group: "com.zaxxer", name: "HikariCP", version: "3.4.5"
    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.4'
    compileOnly bungeecord('1.18-R0.1-SNAPSHOT')
}

shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
    relocate('kotlin' as String, project.group + '.portals.bungee.libs.kotlin' as String)
    relocate('org.intellij.lang.annotations' as String, project.group + '.portals.bungee.libs.annotations.intellij' as String)
    relocate('org.jetbrains.annotations' as String, project.group + '.portals.bungee.libs.annotations.jetbrains' as String)
    relocate('com.zaxxer.hikari' as String, project.group + '.portals.core.libs.hikari' as String)
    relocate('org.slf4j' as String, project.group + '.portals.bungee.libs.slf4j' as String)
}

runBungee {
    args '--nojline', '--max-players', '100'
    jvmArgs '-Xmx512M'
}

artifacts {
    archives shadowJar
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.WARN)
    inputs.property "version", project.version
    from(sourceSets.main.resources.srcDirs) {
        include 'bungee.yml'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'bungee.yml'
    }
}