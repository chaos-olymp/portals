plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.kapt'
}

group 'de.chaosolymp'
version rootProject.version

repositories {
    mavenCentral()
}

dependencies {
    compileOnly project(":portals-annotations")
    implementation group: "com.zaxxer", name: "HikariCP", version: "3.4.5"
    kapt project(":portals-annotation-processor")

    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'com.google.guava:guava:31.0.1-jre'
    testImplementation "org.testcontainers:testcontainers:1.16.2"
    testImplementation "org.testcontainers:junit-jupiter:1.16.2"
    testImplementation 'org.testcontainers:mysql:1.16.2'
    testImplementation 'mysql:mysql-connector-java:8.0.27'
    testImplementation 'org.slf4j:slf4j-api:1.6.1'
    testImplementation 'org.slf4j:slf4j-simple:1.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
    relocate('kotlin' as String, project.group + '.portals.core.libs.kotlin' as String)
    relocate('com.zaxxer.hikari' as String, project.group + '.portals.core.libs.hikari' as String)
    relocate('org.intellij.lang.annotations' as String, project.group + '.portals.core.libs.annotations.intellij' as String)
    relocate('org.jetbrains.annotations' as String, project.group + '.portals.core.libs.annotations.jetbrains' as String)
    relocate('com.google.thirdparty' as String, project.group + '.portals.core.libs.annotations.google_thirdparty' as String)
}

sourceSets {
    main {
        java {
            srcDir "${buildDir.absolutePath}/generated/source/kaptKotlin/"
        }
    }
}


artifacts {
    archives shadowJar
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.WARN)
    inputs.property "version", project.version
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}